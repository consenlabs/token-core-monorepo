apply plugin: 'maven-publish'

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def NEXUS_USER = properties.getProperty('NEXUS_USER')
def NEXUS_PASSWORD = properties.getProperty('NEXUS_PASSWORD')
def VERSION = properties.getProperty('VERSION')

version VERSION
group 'im.token.libs'

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                artifact("$buildDir/outputs/aar/tokencore-release.aar")

                groupId this.group
                artifactId 'token-core'
                version this.version

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    configurations.implementation.allDependencies.each {
                        if (it.name != 'unspecified') {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name 'nexus'
                url "https://nexus.token.im/repository/im-releases/"
                credentials {
                    username NEXUS_USER
                    password NEXUS_PASSWORD
                }
            }
        }
    }
}
