// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("maven-publish")
    id("signing")
    id("org.jreleaser") version "1.18.0"
}

version System.getenv("VERSION") ?: project['version']
group 'io.github.consenlabs.android'

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                artifact("tokencore/build/outputs/aar/tokencore-release.aar")

                groupId this.group
                artifactId 'token-core'
                version this.version

                pom {
                    name = "token-core"
                    description = "a android library for web3"
                    url = "https://github.com/consenlabs/token-core-monorepo"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/consenlabs/token-core-monorepo/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "consenlabs"
                            name = "Consenlabs"
                            email = "xuyunzhao@token.im"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/consenlabs/token-core-monorepo"
                        developerConnection = "scm:git:github.com/consenlabs/token-core-monorepo"
                        url = "https://github.com/consenlabs/token-core-monorepo"
                    }
                }
            }
        }
        
        repositories {
            maven {
                name = 'staging'
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }

    signing {
        // Completely disable Gradle signing - JReleaser will handle all signing
        required { false }
        // No signing configuration needed here
    }
}

jreleaser {
    gitRootSearch = true

    project {
        name = 'token-core'
        description = 'A secure and efficient Android library for Web3 wallet management, supporting multiple blockchains and token standards'
        website = 'https://github.com/consenlabs/token-core-monorepo'
        license = 'Apache-2.0'
        java {
            groupId = this.group
            version = this.version
        }
    }

    signing {
        active = 'ALWAYS'
        armored = true
    }
    
    deploy {
        maven {
            nexus2 {
                maven {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepository('build/staging-deploy')
                    sign = true
                }
            }
        }
    }
}
