// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.2.2'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    id("maven-publish")
    id("signing")
    id("org.jreleaser") version "1.10.0"
}

version System.getenv("VERSION") ?: project['version']
group 'io.github.consenlabs.android'

afterEvaluate {
    publishing {
        publications {
            Production(MavenPublication) {
                artifact("tokencore/build/outputs/aar/tokencore-release.aar")

                groupId this.group
                artifactId 'token-core'
                version this.version

                pom {
                    name = "token-core"
                    description = "a android library for web3"
                    url = "https://github.com/consenlabs/token-core-monorepo"
                    licenses {
                        license {
                            name = "Apache License 2.0"
                            url = "https://github.com/consenlabs/token-core-monorepo/blob/main/LICENSE"
                        }
                    }
                    developers {
                        developer {
                            id = "consenlabs"
                            name = "Consenlabs"
                            email = "xuyunzhao@token.im"
                        }
                    }
                    scm {
                        connection = "scm:git:github.com/consenlabs/token-core-monorepo"
                        developerConnection = "scm:git:github.com/consenlabs/token-core-monorepo"
                        url = "https://github.com/consenlabs/token-core-monorepo"
                    }
                }
            }
        }
    }

    signing {
        //make signing required unless for SNAPSHOT releases or if signing is explicitly skipped
        required { !project.version.endsWith("-SNAPSHOT") && !project.hasProperty("skipSigning") }

        //look for property 'signingKey'
        if (project.findProperty("signingKey")) {
            //If required, read a sub-key specified by its ID in property signingKeyId
            //def signingKeyId = findProperty("signingKeyId")
            //read property 'signingKey'
            def signingKey = findProperty("signingKey")
            //read property 'signingPassword'
            def signingPassword = findProperty("signingPassword")
            //Select to use in-memory ascii-armored keys
            useInMemoryPgpKeys(signingKey, signingPassword)
            //Only if also using signingKeyId
            //useInMemoryPgpKeys(signingKeyId, signingKey, signingPassword)

            //Apply signing to publication identity 'publishing.publications.maven'
            sign publishing.publications.Production
        } else {
            sign publishing.publications.Production
            // println 'WARNING: No property \'signingKey\' found. Artifact signing will be skipped.'
        }
    }
}

jreleaser {
    project {
        name = 'token-core'
        description = 'A android library for web3'
        website = 'https://github.com/consenlabs/token-core-monorepo'
        license = 'Apache-2.0'
        java {
            groupId = this.group
            version = this.version
        }
    }
    
    deploy {
        maven {
            // 配置OSSRH中央仓库
            nexus2 {
                maven {
                    active = 'ALWAYS'
                    url = 'https://s01.oss.sonatype.org/service/local'
                    // 使用GitHub Actions的环境变量
                    // 用户名和密码在workflow文件中设置为环境变量
                    username = '{{jreleaser.nexus2.username}}'
                    password = '{{jreleaser.nexus2.password}}'
                    closeRepository = true
                    releaseRepository = true
                    stagingRepositories = ['build/staging-deploy']
                    // 要发布的产物
                    publication = 'Production'
                    // 是否要签名，默认为true
                    sign = true
                    // 使用GPG签名
                    gpg {
                        // 使用环境变量中定义的GPG密钥对
                        publicKey = '{{jreleaser.gpg.public.key}}'
                        secretKey = '{{jreleaser.gpg.secret.key}}'
                        passphrase = '{{jreleaser.gpg.passphrase}}'
                    }
                }
            }
        }
    }
}
